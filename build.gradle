plugins {
    id "maven-publish"
    id "java"
    id "java-library"
    id "com.github.johnrengelman.shadow"  version "8.1.1"
    id "cr_puzzle_gradle" version "1.0.60-dev"
}

group = "dev.crmodders"

puzzle_loader {
    accessManipulatorPath = file("src/main/resources/puzzle_loader.manipulator")
//    fabricAccessWidenerPath = file("src/main/resources/puzzle_loader.accesswidener")
//    forgeAccessTransformerPath = file("src/main/resources/META-INF/accesstransformer.cfg")
}

repositories {
    mavenCentral()

    maven { url "https://jitpack.io" }
    maven { url "https://maven.google.com/" }
    maven { url "https://maven.crmodders.dev/releases" }
    maven { url "https://maven.crmodders.dev/snapshots" }
    maven { url "https://repo.spongepowered.org/repository/maven-public/" }
}

// do not remove fucks up building
tasks.buildBundleJar.configure {dependsOn(tasks.compileJava)}

dependencies {
    implementation 'org.jetbrains:annotations:24.0.0'

//     Mixin
    compileOnly("org.spongepowered:mixin:$mixinVersion")
    bundle(annotationProcessor("io.github.llamalad7:mixinextras-common:$mixinExtrasVersion"))

//     Google Stuffs
    bundle("com.google.guava:guava:$guavaVersion")
    bundle("com.google.code.gson:gson:$gsonVersion")

    // Asm
    compileOnly("org.ow2.asm:asm:$asmVersion")
    compileOnly("org.ow2.asm:asm-tree:$asmVersion")
    compileOnly("org.ow2.asm:asm-util:$asmVersion")
    compileOnly("org.ow2.asm:asm-analysis:$asmVersion")
    compileOnly("org.ow2.asm:asm-commons:$asmVersion")

    // Log4j
    bundle("org.apache.logging.log4j:log4j-api:$log4jVersion")
    bundle("org.apache.logging.log4j:log4j-core:$log4jVersion")
    bundle("org.apache.logging.log4j:log4j-slf4j2-impl:$log4jVersion")

    // Jopt Simple
    bundle("net.sf.jopt-simple:jopt-simple:$joptSimpleVersion")

    // Reflections
    bundle("org.reflections:reflections:$reflectionsVersion")

    // NBT
    bundle("dev.dewy:nbt:$nbtVersion")

    // HJson
    bundle("org.hjson:hjson:$hjsonVersion")

//     Jython
//    implementation("org.python:jython-slim:2.7.3")

//     Lua J
//    implementation("party.iroiro.luajava:luajit:$luaJVersion")
//    implementation("party.iroiro.luajava:luajit-platform:$luaJVersion:natives-desktop")
}

tasks.register("runLoader", JavaExec) {
    group = "loader"

    classpath = sourceSets.main.runtimeClasspath
    mainClass = "dev.crmodders.puzzle.loader.launch.Piece"
}

publishing {
    repositories {
        maven {
            name = "crmReleases"
            url = "https://maven.crmodders.dev/releases"
            credentials{
                username = System.getenv("CRMReleasesUsername")
                password = System.getenv("CRMReleasesPassword")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    assemble.dependsOn buildSourcesJar, buildBundleJar
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = id

            artifact source: buildBundleJar, classifier: '', extension: 'jar'
            artifact source: buildSourcesJar, classifier: 'source', extension: 'jar'
        }
    }
}
